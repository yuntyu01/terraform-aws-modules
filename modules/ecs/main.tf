# 0. Global & Base Resources
# 1. Security Groups 
# 2. ALB & Target Group
# 3. IAM Roles (ECS Node & Task Roles & ssm)
# 4. ECS Cluster & Compute (Cluster, LT, ASG, CP)
# 5. ECS Task & Service

terraform {
  required_providers {
    aws = {
      source  = "hashicorp/aws"
      version = "~> 6.0"
    }
  }
}


# ==============================================================================
# 0. Global & Base Resources
# ==============================================================================
# CloudFrontì˜ IP ëŒ€ì—­
data "aws_ec2_managed_prefix_list" "cloudfront" {
  name = "com.amazonaws.global.cloudfront.origin-facing"
}

# aws ê³„ì •ì •ë³´ ì¡°íšŒ (account_id ì‚¬ìš©ì„ ìœ„í•´)
data "aws_caller_identity" "current" {}

# CloudWatch Log Group
resource "aws_cloudwatch_log_group" "ecs" {
  name              = "/${var.name}/ecs"
  retention_in_days = 7 #7ì¼ í›„ ì‚­ì œ
}


# ==============================================================================
# 1. Security Groups 
# ==============================================================================

# 1) ALBìš© ë³´ì•ˆ ê·¸ë£¹ (ì™¸ë¶€ 80/443 í—ˆìš©)
resource "aws_security_group" "alb_sg" {
  name        = "${var.name}-alb-sg"
  description = "Security group for public ALB"
  vpc_id      = var.vpc_id

  ingress {
    from_port   = 80
    to_port     = 80
    protocol    = "tcp"
    prefix_list_ids = [data.aws_ec2_managed_prefix_list.cloudfront.id]
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  tags = { Name = "${var.name}-alb-sg" }
}

# 2) ECS ë…¸ë“œ(EC2)ìš© ë³´ì•ˆ ê·¸ë£¹ (ALB íŠ¸ë˜í”½ í—ˆìš©)
resource "aws_security_group" "ecs_node_sg" {
  name        = "${var.name}-ecs-node-sg"
  description = "Security group for ECS Nodes"
  vpc_id      = var.vpc_id

  # ë™ì  í¬íŠ¸ ë§¤í•‘ì„ ìœ„í•´ ALBì—ì„œ ì˜¤ëŠ” ëª¨ë“  í¬íŠ¸(0-65535) í—ˆìš©
  ingress {
    from_port       = 0
    to_port         = 65535
    protocol        = "tcp"
    security_groups = [aws_security_group.alb_sg.id]
    description     = "Allow All Traffic from ALB for Dynamic Port Mapping"
  }

  # SSH ì ‘ì† 
  ingress {
    from_port   = 22
    to_port     = 22
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"] 
    description = "Allow SSH"
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  tags = { Name = "${var.name}-ecs-node-sg" }
}


# ==============================================================================
# 2. ALB & Target Group
# ==============================================================================

resource "aws_lb" "main" {
  name               = "${var.name}-lb"
  internal           = false
  load_balancer_type = "application"
  security_groups    = [aws_security_group.alb_sg.id]
  subnets            = var.public_subnet_ids

  tags = { Name = "${var.name}-lb" }
}

resource "aws_lb_target_group" "lb_tg" {
  name        = "${var.name}-tg"
  port        = 80
  protocol    = "HTTP"
  vpc_id      = var.vpc_id
  target_type = "instance" # ECS EC2ëª¨ë“œëŠ” instance íƒ€ì… í•„ìˆ˜

  health_check {
    path                = "/health" # í˜¹ì€ /actuator/health
    protocol            = "HTTP"
    interval            = 30
    timeout             = 5
    healthy_threshold   = 2
    unhealthy_threshold = 2
    matcher             = "200"
  }

  tags = { Name = "${var.name}-tg" }
}

resource "aws_lb_listener" "http" {
  load_balancer_arn = aws_lb.main.arn
  port              = "80"
  protocol          = "HTTP"

  default_action {
    type             = "forward"
    target_group_arn = aws_lb_target_group.lb_tg.arn
  }
}

# ==============================================================================
# 3. IAM Roles (ECS Node & Task Roles & ssm & SSM Parameter Store)
# ==============================================================================

# 3-1. EC2 Instance Role (ECS Agent)
# ECS ì—ì´ì „íŠ¸ìš© ì—­í• 
resource "aws_iam_role" "ecs_instance_role" {
  name = "${var.name}-ecs-instance-role"
  assume_role_policy = jsonencode({
    Version = "2012-10-17"
    Statement = [{
      Action = "sts:AssumeRole"
      Effect = "Allow"
      Principal = { Service = "ec2.amazonaws.com" }
    }]
  })
}

# ECS ì—ì´ì „íŠ¸ìš© í•„ìˆ˜ ì •ì±… ì—°ê²°
resource "aws_iam_role_policy_attachment" "ecs_instance_role_attach" {
  role       = aws_iam_role.ecs_instance_role.name
  policy_arn = "arn:aws:iam::aws:policy/service-role/AmazonEC2ContainerServiceforEC2Role"
}

# EC2ìš© ì¸ìŠ¤í„´ìŠ¤ í”„ë¡œíŒŒì¼
resource "aws_iam_instance_profile" "ecs_instance_profile" {
  name = "${var.name}-ecs-instance-profile"
  role = aws_iam_role.ecs_instance_role.name
}


# 3-2. ECS Task Execution Role (ì´ë¯¸ì§€ Pull, ë¡œê·¸ ì €ì¥)
resource "aws_iam_role" "ecs_exec_role" {
  name = "${var.name}-ecs-exec-role"
  assume_role_policy = jsonencode({
    Version = "2012-10-17"
    Statement = [{
      Action = "sts:AssumeRole"
      Effect = "Allow"
      Principal = { Service = "ecs-tasks.amazonaws.com" }
    }]
  })
}
resource "aws_iam_role_policy_attachment" "ecs_exec_role_attach" {
  role       = aws_iam_role.ecs_exec_role.name
  policy_arn = "arn:aws:iam::aws:policy/service-role/AmazonECSTaskExecutionRolePolicy"
}

# 3-3. ECS Task Role (ì•±ì´ S3 ì ‘ê·¼í•  ê¶Œí•œ)
resource "aws_iam_role" "ecs_task_role" {
  name = "${var.name}-ecs-task-role"
  assume_role_policy = jsonencode({
    Version = "2012-10-17"
    Statement = [{
      Action = "sts:AssumeRole"
      Effect = "Allow"
      Principal = { Service = "ecs-tasks.amazonaws.com" }
    }]
  })
}

# S3 ì ‘ê·¼ ì •ì±…
resource "aws_iam_policy" "s3_access_policy" {
  name        = "${var.name}-s3-policy"
  description = "Allow ECS Task to access S3"
  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [{
      Effect = "Allow"
      Action = ["s3:PutObject", "s3:GetObject", "s3:DeleteObject", "s3:ListBucket"]
      Resource = [
        "arn:aws:s3:::${var.bucket_name}",
        "arn:aws:s3:::${var.bucket_name}/*"
      ]
    }]
  })
}
resource "aws_iam_role_policy_attachment" "ecs_task_s3_attach" {
  role       = aws_iam_role.ecs_task_role.name
  policy_arn = aws_iam_policy.s3_access_policy.arn
}

# CloudWatch Agent ì§€í‘œ ìˆ˜ì§‘ ì •ì±…
resource "aws_iam_role_policy_attachment" "ecs_instance_cw_agent_policy" {
  role       = aws_iam_role.ecs_instance_role.name
  policy_arn = "arn:aws:iam::aws:policy/CloudWatchAgentServerPolicy"
}


######### Session Manage (SSM) ################
# ì ‘ì† í—ˆìš© ì •ì±… ì—°ê²°
resource "aws_iam_role_policy_attachment" "ecs_instance_ssm" {
  role       = aws_iam_role.ecs_instance_role.name
  policy_arn = "arn:aws:iam::aws:policy/AmazonSSMManagedInstanceCore"
}

resource "aws_iam_policy" "ecs_exec_ssm" {
  name        = "${var.name}-ecs-exec-ssm-policy"
  description = "Allow ECS Exec to container"
  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Effect = "Allow"
        Action = [
          "ssmmessages:CreateControlChannel",
          "ssmmessages:CreateDataChannel",
          "ssmmessages:OpenControlChannel",
          "ssmmessages:OpenDataChannel"
        ]
        Resource = "*"
      }
    ]
  })
}

# ìœ„ ì •ì±…ì„ Task Roleì— ì—°ê²°
resource "aws_iam_role_policy_attachment" "ecs_exec_attach" {
  role       = aws_iam_role.ecs_task_role.name
  policy_arn = aws_iam_policy.ecs_exec_ssm.arn
}

########### SSM Parameter Store ##############
resource "aws_iam_policy" "ecs_ssm_read" {
  name        = "${var.name}-ecs-ssm-read-policy"
  description = "Allow ECS Task Execution Role to read SSM Parameters"

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Effect = "Allow"
        Action = [
          "ssm:GetParameters",
          "ssm:GetParameter",
          "kms:Decrypt" # SecureString ë³µí˜¸í™”ì— í•„ìš”
        ]
        # íŠ¹ì • ê²½ë¡œì˜ íŒŒë¼ë¯¸í„°ë§Œ ì½ë„ë¡ ì œí•œ (ë³´ì•ˆ ê¶Œì¥)
        Resource = "arn:aws:ssm:${var.region}:${data.aws_caller_identity.current.account_id}:parameter/${var.name}/*"
      }
    ]
  })
}

# 2. ì •ì±…ì„ ecs_exec_roleì— ì—°ê²° (ê¸°ì¡´ exec roleì— ì—°ê²°í•´ì•¼ í•¨)
resource "aws_iam_role_policy_attachment" "ecs_exec_ssm_read_attach" {
  role       = aws_iam_role.ecs_exec_role.name
  policy_arn = aws_iam_policy.ecs_ssm_read.arn
}

# ==============================================================================
# 4. ECS Cluster & Compute (Cluster, LT, ASG, CP)
# ==============================================================================

resource "aws_ecs_cluster" "main" {
  name = "${var.name}-cluster"
}

resource "aws_launch_template" "ecs_lt" {
  name_prefix   = "${var.name}-ecs-lt"
  image_id      = var.ami_id
  instance_type = var.instance_type 
  key_name      = var.key_name

  iam_instance_profile {
    name = aws_iam_instance_profile.ecs_instance_profile.name
  }

  network_interfaces {
    associate_public_ip_address = false
    security_groups             = [aws_security_group.ecs_node_sg.id]
  }
#     # ğŸ’¡ [í•µì‹¬ ì¶”ê°€] yum ì ê¸ˆ(lock)ì´ í’€ë¦´ ë•Œê¹Œì§€ ëŒ€ê¸° ë° ë„¤íŠ¸ì›Œí¬ í™•ì¸
#     echo "Waiting for yum lock to be released..."
#     while fuser /var/lib/rpm/.rpm.lock >/dev/null 2>&1; do sleep 5; done
    
#     # ğŸ’¡ [í•µì‹¬ ì¶”ê°€] ì„¤ì¹˜ ì‹¤íŒ¨ ì‹œ ìµœëŒ€ 5ë²ˆ ì¬ì‹œë„ ë¡œì§
#     for i in {1..5}; do
#       sudo yum install -y amazon-cloudwatch-agent && break || sleep 10
#     done
  user_data = base64encode(<<-EOF
    #!/bin/bash
    echo ECS_CLUSTER=${aws_ecs_cluster.main.name} >> /etc/ecs/ecs.config
    # 2. ì—ì´ì „íŠ¸ ì„¤ì¹˜
    sudo yum install -y amazon-cloudwatch-agent

    # 3. ë©”ëª¨ë¦¬ ì§€í‘œ 1ê°œë§Œ ìˆ˜ì§‘í•˜ëŠ” ì„¤ì • íŒŒì¼ ìƒì„±  
    cat <<'JSON' > /opt/aws/amazon-cloudwatch-agent/bin/config.json
    {
      "agent": {
        "metrics_collection_interval": 60,
        "run_as_user": "root"
      },
      "metrics": {
        "namespace": "CWAgent",
        
        "aggregation_dimensions": [
          ["AutoScalingGroupName"]
        ],

        "append_dimensions": {
          "AutoScalingGroupName": "$${aws:AutoScalingGroupName}",
          "InstanceId": "$${aws:InstanceId}"
        },
        "metrics_collected": {
          "mem": {
            "measurement": [ "mem_used_percent" ],
            "metrics_collection_interval": 60
          }
        }
      }
    }
    JSON

    # 4. ì—ì´ì „íŠ¸ ì‹¤í–‰
    /opt/aws/amazon-cloudwatch-agent/bin/amazon-cloudwatch-agent-ctl \
    -a fetch-config -m ec2 -c file:/opt/aws/amazon-cloudwatch-agent/bin/config.json -s
    
    sudo systemctl enable --now amazon-cloudwatch-agent
    
    EOF
  )

  tag_specifications {
    resource_type = "instance"
    tags = { Name = "${var.name}-ecs-node" }
  }
}

resource "aws_autoscaling_group" "ecs_asg" {
  name                = "${var.name}-ecs-asg"
  vpc_zone_identifier = var.private_subnet_ids
  max_size            = var.asg_max
  min_size            = var.asg_min
  desired_capacity    = var.asg_desired
  
  # ASGê°€ ì»¨í…Œì´ë„ˆê°€ ì¡´ì¬í•˜ëŠ”ì§€ ëª¨ë¥´ê³  EC2ë¥¼ ì‚­ì œí•˜ëŠ” ê²ƒì„ ë°©ì§€
  protect_from_scale_in = true 
  
  launch_template {
    id      = aws_launch_template.ecs_lt.id
    version = "$Latest"
  }

  tag {
    key                 = "AmazonECSManaged"
    value               = true
    propagate_at_launch = true
  }

  enabled_metrics = [
    "GroupMinSize",
    "GroupMaxSize",
    "GroupDesiredCapacity",
    "GroupInServiceInstances",
    "GroupTotalInstances"
  ]
  # "1ë¶„ ê°„ê²©" ëª¨ë‹ˆí„°ë§
  metrics_granularity = "1Minute"
}

resource "aws_ecs_capacity_provider" "main" {
  name = "${var.name}-capacity-provider"

  auto_scaling_group_provider {
    auto_scaling_group_arn = aws_autoscaling_group.ecs_asg.arn
    managed_scaling {
      status          = "ENABLED"
      target_capacity = 100 # ì„œë²„ ê°€ë™ë¥  ëª©í‘œì¹˜
    }
    managed_termination_protection = "ENABLED" # ì„œë²„(ASG) - ì»¨í…Œì´ë„ˆ(CP) íŒ€í‚¬ ë°©ì§€
  }
}

resource "aws_ecs_cluster_capacity_providers" "main" {
  cluster_name       = aws_ecs_cluster.main.name
  capacity_providers = [aws_ecs_capacity_provider.main.name]

  default_capacity_provider_strategy {
    base              = 1
    weight            = 100
    capacity_provider = aws_ecs_capacity_provider.main.name
  }
}

# ==============================================================================
# 5. ECS Task & Service
# ==============================================================================

resource "aws_ecs_task_definition" "app" {
  family                   = "${var.name}-task"
  network_mode             = "bridge" 
  requires_compatibilities = ["EC2"]
  cpu                      = var.cpu
  memory                   = var.memory
  execution_role_arn       = aws_iam_role.ecs_exec_role.arn # ECS Agent - ë¶€íŒ…
  task_role_arn            = aws_iam_role.ecs_task_role.arn # S3 - upload

  container_definitions = jsonencode([
    {
      name      = "${var.name}-container"
      image     = "${var.ecr_repository_url}:latest"
      essential = true

      environment = var.container_env
      secrets = var.container_secrets
      portMappings = [
        {
          containerPort = var.container_port
          hostPort      = 0    # ë™ì í¬íŠ¸
          protocol      = "tcp"
        }
      ],
      logConfiguration = {
        logDriver = "awslogs"
        options = {
          "awslogs-group"         = aws_cloudwatch_log_group.ecs.name
          "awslogs-region"        = var.region
          "awslogs-stream-prefix" = "ecs"
        }
      }
    }
  ])
}

resource "aws_ecs_service" "main" {
  name            = "${var.name}-service"
  cluster         = aws_ecs_cluster.main.id
  task_definition = aws_ecs_task_definition.app.arn
  desired_count   = var.desired_count
  launch_type     = "EC2"
  enable_execute_command = true # ì»¨í…Œì´ë„ˆ ë‚´ë¶€ ì ‘ì†(exec) ê¸°ëŠ¥ í™œì„±í™” (ssm)
  # ë¡œë“œë°¸ëŸ°ì„œ ì—°ê²°
  load_balancer {
    target_group_arn = aws_lb_target_group.lb_tg.arn
    container_name   = "${var.name}-container"
    container_port   = var.container_port
  }

  lifecycle {
    # ì˜¤í† ìŠ¤ì¼€ì¼ë§ê³¼ í…Œë¼í¼ ì‚¬ìš© ì‹œ í•„ìˆ˜
    # Terraformì´ desired_count ë³€ê²½ì„ ê°ì§€í•˜ì§€ ì•Šê²Œ í•¨
    # ì˜¤í† ìŠ¤ì¼€ì¼ë§ìœ¼ë¡œ ì¸ìŠ¤í„´ìŠ¤ ëŠ˜ì–´ë‚˜ê³  ì¬ë°°í¬ì‹œ desired_count ê°¯ìˆ˜ë¡œ ê³ ì •í•˜ëŠ” ë¬¸ì œ í•´ê²°
    ignore_changes = [desired_count]
  }

  # ìˆœì„œ ë³´ì¥ (Listenerê°€ ì—†ìœ¼ë©´ ë°°í¬ ì‹¤íŒ¨í•¨)
  depends_on = [aws_lb_listener.http]
}

# ==============================================================================
# 6. Service Auto Scaling 
# ==============================================================================

# 1. ì˜¤í† ìŠ¤ì¼€ì¼ë§ ëŒ€ìƒ ë“±ë¡ 
resource "aws_appautoscaling_target" "ecs_target" {
  max_capacity       = 5              # ìµœëŒ€ 5ê°œê¹Œì§€ ëŠ˜ì–´ë‚¨
  min_capacity       = 2              # ìµœì†Œ 2ê°œëŠ” ìœ ì§€í•¨
  resource_id        = "service/${aws_ecs_cluster.main.name}/${aws_ecs_service.main.name}"
  scalable_dimension = "ecs:service:DesiredCount"
  service_namespace  = "ecs"
}

# 2. CPU ê¸°ì¤€ ì •ì±…
resource "aws_appautoscaling_policy" "ecs_policy_cpu" {
  name               = "${var.name}-cpu-autoscaling"
  policy_type        = "TargetTrackingScaling"
  resource_id        = aws_appautoscaling_target.ecs_target.resource_id
  scalable_dimension = aws_appautoscaling_target.ecs_target.scalable_dimension
  service_namespace  = aws_appautoscaling_target.ecs_target.service_namespace

  target_tracking_scaling_policy_configuration {
    predefined_metric_specification {
      predefined_metric_type = "ECSServiceAverageCPUUtilization"
    }
    target_value = 70.0 # [ê¸°ì¤€] í‰ê·  CPUê°€ 70%ê°€ ë˜ë„ë¡ ìœ ì§€í•´ë¼
    
    scale_in_cooldown  = 300 # ì¤„ì¼ ë• ì²œì²œíˆ (5ë¶„)
    scale_out_cooldown = 60  # ëŠ˜ë¦´ ë• ë¹ ë¥´ê²Œ (1ë¶„)
  }
}

# 3. ë©”ëª¨ë¦¬ ê¸°ì¤€ ì •ì±… (ë©”ëª¨ë¦¬ê°€ 80% ë„˜ìœ¼ë©´ ëŠ˜ë ¤ë¼!)
resource "aws_appautoscaling_policy" "ecs_policy_memory" {
  name               = "${var.name}-memory-autoscaling"
  policy_type        = "TargetTrackingScaling"
  resource_id        = aws_appautoscaling_target.ecs_target.resource_id
  scalable_dimension = aws_appautoscaling_target.ecs_target.scalable_dimension
  service_namespace  = aws_appautoscaling_target.ecs_target.service_namespace

  target_tracking_scaling_policy_configuration {
    predefined_metric_specification {
      predefined_metric_type = "ECSServiceAverageMemoryUtilization"
    }
    target_value = 80 # [ê¸°ì¤€] í‰ê·  ë©”ëª¨ë¦¬ê°€ 80%ê°€ ë˜ë„ë¡ ìœ ì§€í•´ë¼
  }
}